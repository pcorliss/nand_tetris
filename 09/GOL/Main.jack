class Main {
  function void main() {
    // 32 16-bit segments wide - 512/32
    // 16 16-bit segments wide - 256/32
    // 512 segments total
    var Array a;
    var int i, shapeCounter;

    let a = Array.new(512);
    do Util.initArray(a, 512);

    let shapeCounter = 0;

    // Generate Starter Shapes
    do Shape.pentaDecathalon(a);
    do Shape.pulse(a, 33); // Upper Left
    do Shape.pulse(a, 62); // Upper Right
    do Shape.pulse(a, 449); // Lower Left
    do Shape.pulse(a, 478); // Lower Right

    // Infinite Event Loop
    while(true) {
      // Allow keypresses to generate shapes on the screen
      let i = Keyboard.keyPressed();
      // Cycles the shapes generated from key presses
      if(i > 0){
        if(shapeCounter = 0) {
          do Shape.pulse(a, i * 3);
        }
        if(shapeCounter = 1) {
          do Shape.beacon(a, i * 3);
        }
        if(shapeCounter = 2) {
          do Shape.glider(a, i * 3);
        }
        let shapeCounter = Util.remainder(shapeCounter + 1, 3);
      }

      // Print current display
      do Print.display(a);
      // Increment game board
      do GOL.tick(a);
      // Sleep 250ms
      do Sys.wait(250);
    }
    return;
  }
}
